; Chapter title card
@hidePrinter
@back black
@showUI CC_AT_04
@wait i

@hideUI CC_AT_04
@wait i

@printer c_Dialogue

;DAY3  background should take place in Proxima-IIi's overworld. There's an old warehouse, and the sky is a lit with shooting stars. Most dangerous of the worlds to stay in for long. 
@back Warehouse
@bgm FrozenEchoes

@char ZINNIA

ZINNIA: There's something different about you, compared to the others. 
ZINNIA: Maybe it's that hint of potential you have
YOU: I used to-

@choice "Really admire you." goto:.admire
@choice "Really resent you. " goto:.resent
@stop

# admire
YOU: Company's number one. Aspirational. 
ZINNIA: How do you feel about me now?
YOU: Wouldn't you like to know?
@char ZINNIA.PlayerNeutral
Zinnia stares at you. 
You don't reply. 
@goto .atmosphere

# resent
@char ZINNIA.PlayerSmile
ZINNIA: No one's ever said that to my face before. 
Zinnia almost looks pleased. 
YOU: I like you more now. You're a lot cuter than I thought you could be. 
YOU: When all the glitz and glamour fades, you're just a person. 
ZINNIA: What else could I be? 
YOU: A genetic trash super soldier? 
ZINNIA: Ahaha! 
@goto .atmosphere

# atmosphere
@hide ZINNIA
There's something about the atmosphere here that makes you feel off, maybe a little bit more brutally honest. Uncensored. And maybe, just a bit dizzy. 

YOU: My head's pounding. It hurts. 

Your vision blurs. There's static ringing in your ears, but Zinnia's voice rings louder.

@char ZINNIA.PlayerNeutral
ZINNIA: You've really been a first class bottom feeder, if you aren't used to these oxygen levels.
ZINNIA: This isn't just a standard routine planetary rotation.
ZINNIA: Do you know why we do three planets per run? This used to be a pilgrimage. A sacred rite.
YOU: That's just what people say to get cheap labor. 
ZINNIA: Maybe. 
ZINNIA: The old spacefaring ways were different though. You're fighting it right now, but I think you'll get it soon enough. 
ZINNIA: There's potential in you, compared to the others. 
ZINNIA: I think. You could almost be special. 
ZINNIA: I didn't think there was someone else like me before. 

You don't say anything. Hoping that she'll say something more. 

She doesn't. 

Her silence says get to work. 

@stopBgm

# itemPick
@bgm SubterraneanGlistening
@hide ZINNIA

You collect:

@choice "ITEM: DEVOTIONAL OIL" goto:.oil
@choice "ITEM: OLD EARTHENWARE" goto:.earthenware
@choice "ITEM: BROKEN CONTROLLER" goto:.controller
@stop

# oil

ITEM DESCRIPTION: Some sort of oil; it's greasy to the touch. Easily flammable. 

An interesting thing to fish from the bottom of water. You can't quite make out its label, the words all but worn out and faded from time. The bottle still feels heavy in your hands. 
@char ZINNIA.PlayerNeutral
YOU: It looks like a bottle.
YOU: Drone diagnostic scans are saying a bottle of oil…
YOU: Why would that be over here?
ZINNIA: It's devotional. It's a special grade; there used to be a drilling facility here for this type of oil.
ZINNIA: Stuff like this was blessed by gods, before it was all dried up. 
MARIA: ....

DO YOU WANT TO COLLECT THIS? 

ONE YOU COLLECT AN ITEM, YOU'LL BE UNABLE TO TAKE ANYTHING ELSE BACK TO BASE. 

COLLECT THE “DEVOTIONAL OIL?”

@choice "YES" goto:.oilGet
@choice "NO" goto:.oilNo
@stop

# oilGet
@set AT_RouteA++
@set HasDevotionalOil=true
ZINNIA: The OA team is gonna appreciate it.   
YOU: This stuff gets analyzed? 
ZINNIA: Sometimes.
ZINNIA: Oil like this, was used to revere. 
ZINNIA: Made to worship.  
@goto .itemGet

# oilNo
ZINNIA: You're tossing it back in?
@char ZINNIA.EyesClosed
She almost sounds pained.
YOU: We can always fish it out later. 
ZINNIA: ….
ZINNIA: Right. 
@goto .itemPick

# earthenware
ITEM DESCRIPTION: In a world full of devices built from machines. This looks special. This looks worthless. This looks entirely made from hand. 

@char ZINNIA.Aloof
ZINNIA: ....
YOU: You don't seem impressed. 
ZINNIA: Old “earthenware.”
YOU:  Even if it's not gonna land us money in the bank, it's a cute decoration. 
YOU: I love her. 
ZINNIA: …

DO YOU WANT TO COLLECT THIS? 

ONE YOU COLLECT AN ITEM, YOU'LL BE UNABLE TO TAKE ANYTHING ELSE BACK TO BASE. 

COLLECT THE “OLD EARTHENWARE?”

@choice "YES" goto:.earthenwareGet
@choice "NO" goto:.earthenwareNo
@stop

# earthenwareGet
@set AT_RouteB++
@set HasOldEarthenware=true
ZINNIA: You're really bringing this back. 
YOU: Yeah?
ZINNIA: You just can't. 
ZINNIA: Nevermind. 
@goto .itemGet

# earthenwareNo
YOU: Hold on. 
YOU: What if I find something I love even more?
@goto .itemPick

# controller
ITEM DESCRIPTION: It's a broken controller. You think it was used for music. Some sick warehouse raves. 
@char ZINNIA.PlayerNeutral
YOU: I guess they threw parties here.
ZINNIA: What was your first guess?
MARIA: A vintage Pioneer, nice.  

DO YOU WANT TO COLLECT THIS? 

ONE YOU COLLECT AN ITEM, YOU'LL BE UNABLE TO TAKE ANYTHING ELSE BACK TO BASE. 

COLLECT THE “BROKEN CONTROLLER?”

@choice "YES" goto:.controllerGet
@choice "NO" goto:.controllerno
@stop

# controllerGet
@set AT_RouteC++
@set HasBrokenController=true
YOU: C'mon, we can rig this up and party on the ship.  
ZINNIA: ...
You can feel her rolling her eyes. 
@goto .itemGet

# controllerno
YOU: Hmm…
YOU: I'll be back!!
@goto .itemPick

# itemGet
@char ZINNIA.PlayerSmile
ZINNIA: Solid collection. Let's head back to the ship. 

@stopBgm
@hide ZINNIA
@hidePrinter
@back black
@wait i 

@goto at_interimfour
@stop